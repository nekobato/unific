###

  StreamEvent.coffee

###

module.exports.StreamEvent = (app) ->

  async  = require 'async'
  RSS    = require 'rss'
  _      = require 'underscore'
  url    = require 'url'
  debug  = require('debug')('events/stream')

  Stream = app.get("models").Stream
  Feed   = app.get("models").Feed
  Page   = app.get("models").Page
  Subscribe   = app.get('models').Subscribe

  HelperEvent = app.get('events').HelperEvent app

  # HTTP EndPoint - Stream
  index: (req,res,next) ->
    title = req.params.stream
    Stream.findOne {title:title},(err,stream)->
      if err
        debug(err)
        return res.send 400,'Internal Server Error'

      if stream?
        return render(res,stream)

      else
        Stream.create
          title:title
          description:'description (click to edit)'
        ,(err,stream)->
          return render(res,stream)

  # HTTP Endpoint - RSS
  rss  : (req,res,next) ->
    title = req.params.stream
    HelperEvent.getArticlesByStreamWithLimit title,100,(err,articles)->
      return HelperEvent.httpError err,res if err
      return res.send 404,"Articles Not Found" if articles.length is 0

      feed = new RSS
        title: "#{title} - Unific"
        description: "Generated By Unific"
        feed_url: "http://unific.net/#{title}/rss"
        site_url: "http://unific.net/#{title}"
        author: "Unific"
        webMaster: "Unific"
        copyright: "2014 unific.net"
        pubDate: articles[0].page.pubDate


      for article in articles
        feed.item
          title: article.page.title
          description:article.page.description
          url: article.page.url
          author: article.feed.title # optional - defaults to feed author property
          date: article.page.pubDate

      xml = feed.xml()
      res.set
        "Content-Type": "text/xml"
      res.send xml

  ###
  # socket.io events
  ###
  subscribeFeed:(socket,io,data)->
    streamName = decodeURIComponent data.stream
    Stream.findOne title:streamName,(err,stream)->
      return HelperEvent.ioError err,socket if err or not stream
      data.stream = stream

      Feed.findOneAndUpdate
        url       : data.feed.url
      ,
        title     : data.feed.title
        sitename  : data.feed.sitename
        url       : data.feed.url
        favicon   : data.feed.favicon
        siteUrl   : data.feed.link or data.feed.siteUrl or data.feed.url.split(data.feed.href)[0]
      , upsert    : true ,(err,feed)->
        return HelperEvent.ioError(err,socket) if err or not feed

        if stream.feeds.indexOf feed._id is -1
          stream.feeds.push feed._id
          stream.save()

        # データ更新&watcher
        # @todo ここらへんかなり密になっててやばい
        crowler     = app.get('crowler')
        crowler.addToSet feed
        return io.of("#{stream.title}").emit 'subscribedFeed',feed

  unsubscribeFeed:(socket,io,data)->
    streamName = decodeURIComponent data.stream
    Stream.findOne title:streamName,(err,stream)->
      return HelperEvent.ioError err,socket if err or not stream
      stream.feeds.pull data.feed._id
      stream.save()
      return io.of("/#{stream.title}").emit 'unsubscribedFeed',feed

  changeDesc: (socket,data) ->
    streamName = decodeURIComponent data.stream
    Stream.findOne title:streamName, (err,stream)->
      if err
        debug err
        return HelperEvent.error error,socket
      stream.description = data.text
      stream.save()
      socket.broadcast.to(data.stream).emit 'descChanged',
        text:data.text

###
# Private Methods
###
render = (res,stream)->
  res.render 'stream',
    stream:stream
